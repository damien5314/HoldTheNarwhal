apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.fabric'

// TODO: Remove conditional once below issue is resolved
// https://github.com/KeepSafe/dexcount-gradle-plugin/issues/235
def taskRequests = getGradle().getStartParameter().getTaskRequests()
if (!taskRequests.toString().toLowerCase().contains("debug")) {
    apply plugin: 'com.getkeepsafe.dexcount'
}

allprojects {
    dependencyUpdates.resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'snapshot', 'nightly'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Ignoring release candidates')
                }
            }
        }
    }
}

repositories {
    jcenter()
    maven { url "https://maven.google.com/" }
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

Properties localProperties = new Properties()
localProperties.load(project.rootProject.file("local.properties").newDataInputStream())

android {
    compileSdkVersion COMPILE_SDK_VERSION

    useLibrary "org.apache.http.legacy"

    defaultConfig {
        applicationId "com.ddiehl.android.htn"
        minSdkVersion 15
        targetSdkVersion TARGET_SDK_VERSION
        versionCode 19
        versionName "0.14"
        resConfigs "en"
        buildConfigField "long", "BUILD_TIME_UTC", "" + getTime()
        def appId = localProperties.getProperty("app_id")
        buildConfigField "String", "REDDIT_APP_ID", "\"" + appId + "\""
        def redirectUri = localProperties.getProperty("app_redirect_uri")
        buildConfigField "String", "REDDIT_REDIRECT_URI", "\"" + redirectUri + "\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile file('keys/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release
    }

    Properties releaseKeystoreProperties = getProperties("app/keys/release/keystore.properties")
    if (releaseKeystoreProperties != null) {
        signingConfigs.release.storeFile = file(releaseKeystoreProperties['storeFile'])
        signingConfigs.release.storePassword = releaseKeystoreProperties['storePassword']
        signingConfigs.release.keyAlias = releaseKeystoreProperties['keyAlias']
        signingConfigs.release.keyPassword = releaseKeystoreProperties['keyPassword']
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-dev"
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Renames APK to our standard format `buildType-versionName_versionCode-RCx.apk
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "${variant.buildType.name}-${variant.versionName}_${variant.versionCode}-RC0.apk"
        }
    }
}

dependencies {
    implementation fileTree(include: "*.jar", dir: "libs")
    implementation project(":logging")

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"

    implementation "androidx.appcompat:appcompat:$ANDROIDX_APPCOMPAT"
    implementation "androidx.recyclerview:recyclerview:$ANDROIDX_RECYCLERVIEW"
    implementation "androidx.cardview:cardview:$ANDROIDX_CARDVIEW"
    implementation "androidx.constraintlayout:constraintlayout:$ANDROIDX_CONSTRAINTLAYOUT"
    implementation "com.google.android.material:material:$ANDROIDX_MATERIAL"

    implementation("com.crashlytics.sdk.android:crashlytics:$CRASHLYTICS_VERSION@aar") {
        transitive = true
    }

    implementation "com.github.bumptech.glide:glide:$GLIDE_VERSION"
    kapt "com.github.bumptech.glide:compiler:$GLIDE_VERSION"
    implementation "com.github.bumptech.glide:okhttp3-integration:$GLIDE_OKHTTP_VERSION"

    implementation "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    kapt "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    implementation "com.github.damien5314.RxReddit:library:$RXREDDIT_VERSION"
    implementation "com.github.damien5314.RxReddit:android:$RXREDDIT_VERSION"
    debugImplementation project(':mock-api')

    implementation "io.reactivex.rxjava2:rxjava:$RXJAVA_VERSION"
    implementation "io.reactivex.rxjava2:rxandroid:$RXANDROID_VERSION"

    implementation "com.github.damien5314:TimeSinceTextView:$TIMESINCETEXTVIEW_VERSION"

    implementation "com.google.dagger:dagger:$DAGGER_VERSION"
    kapt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    implementation "uk.co.chrisjenx:calligraphy:$CALLIGRAPHY_VERSION"

    implementation "com.hannesdorfmann.fragmentargs:annotation:$FRAGMENTARGS_VERSION"
    kapt "com.hannesdorfmann.fragmentargs:processor:$FRAGMENTARGS_VERSION"

    implementation "com.jakewharton.timber:timber:$TIMBER_VERSION"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$LEAK_CANARY_VERSION"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$LEAK_CANARY_VERSION"

    // Test dependencies
    androidTestImplementation "junit:junit:$JUNIT_VERSION"
    androidTestImplementation "org.mockito:mockito-core:$MOCKITO_VERSION"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    androidTestImplementation 'androidx.annotation:annotation:1.0.0'
    androidTestImplementation 'androidx.test:runner:1.1.0-beta02'
    androidTestImplementation 'androidx.test:rules:1.1.0-beta02'
}

Properties getProperties(def filePath) {
    def propertiesFile = rootProject.file(filePath)
    if (!propertiesFile.exists()) return null

    def properties = new Properties()
    properties.load(new FileInputStream(propertiesFile))
    return properties
}

static def getTime() {
    return (Calendar.getInstance().timeInMillis / 1000).longValue()
}
