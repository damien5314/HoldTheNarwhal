apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.neenbedankt.android-apt'

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.2"
  useLibrary 'org.apache.http.legacy'
  defaultConfig {
    applicationId "com.ddiehl.android.htn"
    minSdkVersion 15
    targetSdkVersion 23
    versionCode 8
    versionName '0.6'
    resConfigs "en"
  }
  signingConfigs {
    release {
      storeFile file(KEYSTORE_PATH)
      storePassword KEYSTORE_PWD
      keyAlias KEY_ALIAS
      keyPassword KEY_PWD
    }
  }
  buildTypes {
    debug {
      minifyEnabled false
      applicationIdSuffix ".debug"
      versionNameSuffix '-dev'
      buildConfigField "String", "REDDIT_APP_ID", "\"" + REDDIT_APP_ID + "\""
      buildConfigField "String", "REDDIT_REDIRECT_URI", "\"" + REDDIT_REDIRECT_URI + "\""
      buildConfigField "String", "FLURRY_API_KEY", "\"" + FLURRY_API_KEY_DEBUG + "\""
    }
    release {
      debuggable false
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
      buildConfigField "String", "REDDIT_APP_ID", "\"" + REDDIT_APP_ID + "\""
      buildConfigField "String", "REDDIT_REDIRECT_URI", "\"" + REDDIT_REDIRECT_URI + "\""
      buildConfigField "String", "FLURRY_API_KEY", "\"" + FLURRY_API_KEY_PROD + "\""
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

def getLocalProperty(String s) {
  Properties properties = new Properties()
  properties.load(project.rootProject.file('local.properties').newDataInputStream())
  return properties.getProperty(s)
}

retrolambda {
  jdk getLocalProperty("jdk8.dir")
  oldJdk getLocalProperty("jdk7.dir")
  javaVersion JavaVersion.VERSION_1_7
}

dependencies {
  compile fileTree(include: '*.jar', dir: 'libs')
  compile project(':bypass')
  def appcompat_version = "23.2.0"
  compile "com.android.support:appcompat-v7:$appcompat_version"
  compile "com.android.support:support-v4:$appcompat_version"
  compile "com.android.support:design:$appcompat_version"
  compile "com.android.support:recyclerview-v7:$appcompat_version"
  compile "com.android.support:cardview-v7:$appcompat_version"
  compile "com.google.android.gms:play-services-analytics:8.4.0" // Required for Flurry
  compile "com.squareup.picasso:picasso:2.5.2"
  compile "com.jakewharton:butterknife:7.0.1"
  def rxreddit_version = "0.7"
  compile "com.github.damien5314.RxReddit:library:$rxreddit_version"
  compile "com.github.damien5314.RxReddit:android:$rxreddit_version"
  debugCompile "com.github.damien5314.RxReddit:mock:$rxreddit_version"
  compile "io.reactivex:rxandroid:1.1.0"
  compile "io.reactivex:rxjava:1.1.0"
  compile "com.github.damien5314:TimeSinceTextView:1.2.3"
  compile "org.parceler:parceler-api:1.0.4"
  apt "org.parceler:parceler:1.0.4"
  compile 'com.jakewharton.timber:timber:4.1.0'
  compile 'com.google.dagger:dagger:2.2'
  apt 'com.google.dagger:dagger-compiler:2.2'
  // Debugging dependencies
  debugCompile "com.squareup.leakcanary:leakcanary-android:1.3.1"
  releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:1.3.1"
}
