apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.github.ben-manes.versions'

android {
  compileSdkVersion 24
  buildToolsVersion "23.0.3"
  useLibrary 'org.apache.http.legacy'

  defaultConfig {
    applicationId "com.ddiehl.android.htn"
    minSdkVersion 15
    targetSdkVersion 24
    versionCode 9
    versionName '0.5.2'
    resConfigs "en"
    buildConfigField "long", "BUILD_TIME_UTC", "" + getTime()
    buildConfigField "String", "REDDIT_APP_ID", "\"" + REDDIT_APP_ID + "\""
    buildConfigField "String", "REDDIT_REDIRECT_URI", "\"" + REDDIT_REDIRECT_URI + "\""
  }

  signingConfigs {
    release {
      storeFile file(KEYSTORE_PATH)
      storePassword KEYSTORE_PWD
      keyAlias KEY_ALIAS
      keyPassword KEY_PWD
    }
  }

  buildTypes {
    debug {
      debuggable true
      minifyEnabled false
      applicationIdSuffix ".debug"
      versionNameSuffix '-dev'
      buildConfigField "String", "FLURRY_API_KEY", "\"" + FLURRY_API_KEY_DEBUG + "\""
    }
    release {
      debuggable false
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
      buildConfigField "String", "FLURRY_API_KEY", "\"" + FLURRY_API_KEY_PROD + "\""
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

def getTime() {
  return (Calendar.getInstance().timeInMillis / 1000).longValue()
}

def getLocalProperty(String s) {
  Properties properties = new Properties()
  properties.load(project.rootProject.file('local.properties').newDataInputStream())
  return properties.getProperty(s)
}

retrolambda {
  jdk getLocalProperty("jdk8.dir")
  oldJdk getLocalProperty("jdk7.dir")
  javaVersion JavaVersion.VERSION_1_7
}

dependencies {
  compile fileTree(include: '*.jar', dir: 'libs')
  compile project(':bypass')
  def appcompat_version = "24.1.0"
  compile "com.android.support:appcompat-v7:$appcompat_version"
  compile "com.android.support:support-v4:$appcompat_version"
  compile "com.android.support:design:$appcompat_version"
  compile "com.android.support:recyclerview-v7:$appcompat_version"
  compile "com.android.support:cardview-v7:$appcompat_version"
  compile "com.google.android.gms:play-services-analytics:9.2.1" // Required for Flurry
  compile "com.squareup.picasso:picasso:2.5.2"
  def butterknife_version = "8.0.1"
  compile "com.jakewharton:butterknife:$butterknife_version"
  apt "com.jakewharton:butterknife-compiler:$butterknife_version"
  def rxreddit_version = "168e694bda8778a423109b9098566049c1988065"
  compile "com.github.damien5314.RxReddit:library:$rxreddit_version"
  compile "com.github.damien5314.RxReddit:android:$rxreddit_version"
  debugCompile "com.github.damien5314.RxReddit:mock:$rxreddit_version"
  compile "io.reactivex:rxandroid:1.2.1"
  compile "io.reactivex:rxjava:1.1.6"
  compile "com.artemzin.rxjava:proguard-rules:1.1.6.0"
  compile "com.github.damien5314:TimeSinceTextView:1.2.3"
  compile "com.jakewharton.timber:timber:4.1.0"
  compile "com.google.dagger:dagger:2.2"
  apt "com.google.dagger:dagger-compiler:2.2"
  // Debugging dependencies
  debugCompile "com.squareup.leakcanary:leakcanary-android:1.3.1"
  releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:1.3.1"
  def fragmentargs_version = "3.0.2"
  compile "com.hannesdorfmann.fragmentargs:annotation:$fragmentargs_version"
  apt "com.hannesdorfmann.fragmentargs:processor:$fragmentargs_version"
  compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha3'
}
