apply plugin: "com.android.application"
apply plugin: "com.jakewharton.hugo"
apply plugin: "com.getkeepsafe.dexcount"
apply plugin: "com.github.ben-manes.versions"
apply plugin: 'io.fabric'

repositories {
    jcenter()
    maven { url "https://maven.google.com/" }
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    useLibrary "org.apache.http.legacy"

    defaultConfig {
        applicationId "com.ddiehl.android.htn"
        minSdkVersion 15
        targetSdkVersion TARGET_SDK_VERSION
        versionCode 15
        versionName "0.10"
        resConfigs "en"
        buildConfigField "long", "BUILD_TIME_UTC", "" + getTime()
        buildConfigField "String", "REDDIT_APP_ID", "\"" + REDDIT_APP_ID + "\""
        buildConfigField "String", "REDDIT_REDIRECT_URI", "\"" + REDDIT_REDIRECT_URI + "\""
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(KEYSTORE_PATH)
            storePassword KEYSTORE_PWD
            keyAlias KEY_ALIAS
            keyPassword KEY_PWD
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-dev"
            buildConfigField "String", "FLURRY_API_KEY", "\"" + FLURRY_API_KEY_DEBUG + "\""
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
            buildConfigField "String", "FLURRY_API_KEY", "\"" + FLURRY_API_KEY_PROD + "\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Renames APK to our standard format `buildType-versionName_versionCode-RCx.apk
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "${variant.buildType.name}-${variant.versionName}_${variant.versionCode}-RC0.apk"
        }
    }
}

static def getTime() {
    return (Calendar.getInstance().timeInMillis / 1000).longValue()
}

def getLocalProperty(String s) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
    return properties.getProperty(s)
}

dependencies {
    compile fileTree(include: "*.jar", dir: "libs")
    compile project(":bypass")
    compile project(":logging")

    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support.constraint:constraint-layout:$CONSTRAINT_LAYOUT_VERSION"

    compile "com.github.bumptech.glide:glide:$GLIDE_VERSION"
    compile "com.github.bumptech.glide:okhttp3-integration:$GLIDE_OKHTTP_VERSION"

    compile "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    compile "com.github.damien5314.RxReddit:library:$RXREDDIT_VERSION"
    compile "com.github.damien5314.RxReddit:android:$RXREDDIT_VERSION"
    debugCompile project(':mock-api')

    compile "io.reactivex.rxjava2:rxjava:$RXJAVA_VERSION"
    compile "io.reactivex.rxjava2:rxandroid:$RXANDROID_VERSION"

    compile "com.github.damien5314:TimeSinceTextView:$TIMESINCETEXTVIEW_VERSION"

    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    annotationProcessor "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    compile "uk.co.chrisjenx:calligraphy:$CALLIGRAPHY_VERSION"

    compile "com.hannesdorfmann.fragmentargs:annotation:$FRAGMENTARGS_VERSION"
    annotationProcessor "com.hannesdorfmann.fragmentargs:processor:$FRAGMENTARGS_VERSION"

    debugCompile "com.squareup.leakcanary:leakcanary-android:$LEAK_CANARY_VERSION"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$LEAK_CANARY_VERSION"

    // Test dependencies
    androidTestCompile "junit:junit:$JUNIT_VERSION"
    androidTestCompile "org.mockito:mockito-core:$MOCKITO_VERSION"
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    androidTestCompile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
}
